

#Область СлужебныеПроцедурыИФункции
Функция ВернутьТолькоЧисла(Знач ТекстовоеСодержимое) Экспорт

	ДлинаСтроки = СтрДлина(ТекстовоеСодержимое);

	ЦенаСтрокой = "";

	Для Индекс = 1 По ДлинаСтроки Цикл

		Символ = Сред(ТекстовоеСодержимое, Индекс, 1);

		Если Символ = "," Тогда

			Прервать;

		КонецЕсли;

		Если Найти("0123456789", Символ) Тогда

			ЦенаСтрокой = ЦенаСтрокой + Символ;

		КонецЕсли;

	КонецЦикла;

	Возврат Число(ЦенаСтрокой);

КонецФункции

Функция ПолучитьЦену(Магазин, Знач Гиперссылка) Экспорт

	Цена = -1;

	ЧтениеHTML = Новый ЧтениеHTML;

	Если Магазин = Справочники.Магазины.OZON Тогда
		
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Гиперссылка, 1);
		
		ПутьКCloudflare = Константы.ПутьКCloudflare.Получить();
		НомерВхождения = СтрНайти(Гиперссылка, "/", НаправлениеПоиска.СКонца, , 1);
		
		ИмяФайла = СтрШаблон("%1.html", Сред(Гиперссылка, НомерВхождения));
		
		ЧтениеHTML= Новый ЧтениеHTML;
		ЧтениеHTML.ОткрытьФайл(СтрШаблон("%1%2", ПутьКCloudflare, ИмяФайла), "UTF-8");
		
	ИначеЕсли Магазин = Справочники.Магазины.СберМегаМаркет Тогда
		
		ПутьКЗенскреб = Константы.ПутьКЗенскреб.Получить();
		HTTPОтвет = ОбщегоНазначения.ОтправитьHTTPЗапрос(СтрШаблон(ПутьКЗенскреб, Гиперссылка));
		КодСтраницы = HTTPОтвет.ПолучитьТелоКакСтроку();
		ЧтениеHTML.УстановитьСтроку(КодСтраницы);

	Иначе

		HTTPОтвет = ОбщегоНазначения.ОтправитьHTTPЗапрос(Гиперссылка);
		КодСтраницы = HTTPОтвет.ПолучитьТелоКакСтроку();
		ЧтениеHTML.УстановитьСтроку(КодСтраницы);

	КонецЕсли;

	ОбъектыDOM = Новый ПостроительDOM;

	ДокументHTML = ОбъектыDOM.Прочитать(ЧтениеHTML);
	
	СчетчикОбхода = 0;
	
	Пока Цена = -1 И СчетчикОбхода >= 0 Цикл

		СчетчикОбхода = СчетчикОбхода + 1;
		
		ОбойтиDOM(Цена, ДокументHTML, Магазин, СчетчикОбхода);
		
	КонецЦикла;
	
	Возврат Цена;

КонецФункции

Процедура ОбойтиDOM(Цена, ДокументHTML, Магазин, СчетчикОбхода)
	
	СтрокиПоиска = ПолучитьСтрокиПоиска(Магазин, СчетчикОбхода);
	
	ЭлементыDOM  = ДокументHTML.ПолучитьЭлементыПоИмени(СтрокиПоиска.ИмяЭлемента);
	
	Для Каждого ЭлементDOM Из ЭлементыDOM Цикл
	
			Если ЭлементDOM.ИмяКласса = СтрокиПоиска.ИмяКласса Тогда
	
				НайденнаяЦена = ВернутьТолькоЧисла(ЭлементDOM.ТекстовоеСодержимое);
	
				Если Цена = -1 Тогда
	
					Цена = НайденнаяЦена;
	
				Иначе
	
					Цена = Мин(Цена, НайденнаяЦена);
					Прервать;
	
				КонецЕсли;
	
			КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСтрокиПоиска(Магазин, СчетчикОбхода)

	Если Магазин = Справочники.Магазины.ЛитРес И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "span";
		ИмяКласса   = "simple-price";

	ИначеЕсли Магазин = Справочники.Магазины.ПризрачныеМиры И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "span";
		ИмяКласса   = "pull-left";

	ИначеЕсли Магазин = Справочники.Магазины.Bookriver И СчетчикОбхода = 1 Тогда // парсит две цены, обычную и со скидкой

		ИмяЭлемента = "span";
		ИмяКласса   = "s1nq5ky_SCPrice";

	ИначеЕсли Магазин = Справочники.Магазины.Литмаркет И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "div";
		ИмяКласса   = "btn btn-success price-btn";

	ИначеЕсли Магазин = Справочники.Магазины.Лабиринт И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "span";
		ИмяКласса   = "buying-pricenew-val-number";

	ИначеЕсли Магазин = Справочники.Магазины.ЧитайГород И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "div";
		ИмяКласса   = "price";

	ИначеЕсли Магазин = Справочники.Магазины.Буквоед И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "div";
		ИмяКласса   = "oD";

	ИначеЕсли Магазин = Справочники.Магазины.Book24 И СчетчикОбхода = 1 Тогда

		ИмяЭлемента = "span";
		ИмяКласса   = "app-price product-sidebar-price__price";
		
	ИначеЕсли Магазин = Справочники.Магазины.OZON И СчетчикОбхода = 1 Тогда
		
		ИмяЭлемента = "span";
		ИмяКласса   = "nv1 n1v";
		
	ИначеЕсли Магазин = Справочники.Магазины.OZON И СчетчикОбхода = 2 Тогда
		
		ИмяЭлемента = "span";
		ИмяКласса   = "nv3 n3v";
		
	ИначеЕсли Магазин = Справочники.Магазины.СберМегаМаркет И СчетчикОбхода = 1 Тогда
		
		ИмяЭлемента = "span";
		ИмяКласса   = "pdp-sales-block__price-final";

	Иначе

		ИмяЭлемента   = "";
		ИмяКласса     = "";
		СчетчикОбхода = -1;

	КонецЕсли;

	Возврат Новый Структура("ИмяЭлемента, ИмяКласса", ИмяЭлемента, ИмяКласса);

КонецФункции
#КонецОбласти